plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'org.sonarqube' version '2.6.1'
}

apply plugin: 'maven'
apply plugin: 'osgi'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'checkstyle'
apply plugin: 'com.github.hierynomus.license'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8


group = 'org.trellisldp'
description = 'Common Classes for Rosid-based Trellis Implementations'

ext {
    moduleName = "org.trellisldp.rosid.common"
    vendor = 'Trellis LDP'
    homepage = 'http://www.trellisldp.org'
    docURL = 'http://www.trellisldp.org/apidocs'
    license = 'Apache 2'

    /* Dependencies */
    trellisApiVersion = '0.4.1-SNAPSHOT'
    trellisVocabularyVersion = '0.2.0'
    commonsCodecVersion = '1.11'
    commonsIoVersion = '2.6'
    commonsRdfVersion = '0.3.0-incubating'
    curatorVersion = '4.0.0'
    jacksonVersion = '2.9.2'
    kafkaVersion = '0.11.0.1_1'
    slf4jVersion = '1.7.25'
    jenaVersion = '3.5.0'
    zookeeperVersion = '3.4.11'

    /* Testing */
    awaitilityVersion = '3.0.0'
    junitVersion = '5.0.2'
    junitPlatformVersion = '1.0.2'
    apiguardianVersion = '1.0.0'
    mockitoVersion = '2.12.0'
    logbackVersion = '1.2.3'
    jacocoVersion = "0.7.9"

    /* OSGi */
    projectOsgiVersion = project.version.replaceAll("-SNAPSHOT", ".SNAPSHOT")
}

configurations {
    buildToolsConfig
}

dependencies {
    api group: 'org.apache.commons', name: 'commons-rdf-api', version: commonsRdfVersion
    api group: 'org.apache.servicemix.bundles', name: 'org.apache.servicemix.bundles.kafka-clients', version: kafkaVersion
    api group: 'org.trellisldp', name: 'trellis-api', version: trellisApiVersion
    api(group: 'org.apache.curator', name: 'curator-recipes', version: curatorVersion) {
        exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    }

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    implementation group: 'commons-codec', name: 'commons-codec', version: commonsCodecVersion
    implementation group: 'commons-io', name: 'commons-io', version: commonsIoVersion
    implementation group: 'org.apache.commons', name: 'commons-rdf-jena', version: commonsRdfVersion
    implementation group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    implementation group: 'org.trellisldp', name: 'trellis-vocabulary', version: trellisVocabularyVersion
    implementation group: 'org.apache.zookeeper', name: 'zookeeper', version: zookeeperVersion

    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    testImplementation group: 'org.apache.curator', name: 'curator-test', version: curatorVersion
    testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: apiguardianVersion
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: junitPlatformVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'org.awaitility', name: 'awaitility', version: awaitilityVersion

    buildToolsConfig 'org.trellisldp:trellis-build-tools:0.2.0'
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    // TODO remove this when appropriate
	maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

release {
    tagTemplate = '$name-$version'

    git {
        signTag = true
    }
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', moduleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "${moduleName};version=${projectOsgiVersion}"
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar
    archives sourceJar
    archives (file('build/resources/main/features.xml')) {
        classifier 'features'
        type 'xml'
    }
}

jacoco {
    toolVersion = jacocoVersion
}

license {
    include "**/*.java"
    header rootProject.file('build/license/HEADER.txt')
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

processResources {
    outputs.upToDateWhen { false }
    expand project.properties
}

signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : ""
        def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ""
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            packaging 'jar'
            url = 'http://www.trellisldp.org'
            inceptionYear '2017'
            name 'Trellis Repository Implementation'
            description 'A high performance implementation of a trellis repository'

            organization {
                name project.vendor
                url project.homepage
            }

            developers {
                developer {
                    id 'acoburn'
                    name 'Aaron Coburn'
                    email 'acoburn @ (domain of organization url)'
                    organization = 'Amherst College'
                    organizationUrl 'https://www.amherst.edu'
                    roles {
                        role 'developer'
                    }
                    timezone '-5'
                }
            }

            scm {
                connection 'scm:git:git://github.com/trellis-ldp/trellis-rosid-common.git'
                developerConnection 'scm:git:git@github.com/trellis-ldp/trellis-rosid-common.git'
                url 'https://github.com/trellis-ldp/trellis-rosid-common'
                tag 'HEAD'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                    comments 'Copyright (c) 2017 Trellis LDP'
                }
            }
        }
    }
}

task processBuildTools(type: Copy) {
    from {
        configurations.buildToolsConfig.collect {
            zipTree(it).matching {
                include 'checkstyle/*.xml'
                include 'license/*.txt'
            }
        }
    }
    into 'build'
}

checkstyle {
    configFile = rootProject.file('build/checkstyle/checkstyle.xml')
    configProperties.checkstyleConfigDir = rootProject.file('build/checkstyle/')
    toolVersion = "8.1"
}

sonarqube {
    properties {
        property "sonar.projectName", "Trellis Repository Rosid Implementation (Common)"
        property "sonar.projectKey", "org.trellisldp:trellis-rosid-common"
        property "sonar.links.homepage", "https://github.com/trellis-ldp/trellis"
        property "sonar.links.issue", "https://github.com/trellis-ldp/trellis-rosid-common/issues"
        property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/trellis-rosid-common.git"
    }
}

task docs(type: Javadoc) {
    outputs.upToDateWhen { false }
    source sourceSets.main.allJava
    classpath = files(sourceSets.main.compileClasspath)
    destinationDir = new File(projectDir, "docs/${version}")
    options {
        links "http://docs.oracle.com/javase/8/docs/api/"
        links "https://commons.apache.org/proper/commons-rdf/apidocs/"
        links "https://kafka.apache.org/0110/javadoc/"
        links "https://trellis-ldp.github.io/trellis/apidocs/"
    }
}

checkstyleMain.dependsOn processBuildTools
afterReleaseBuild.dependsOn uploadArchives
afterReleaseBuild.dependsOn docs

